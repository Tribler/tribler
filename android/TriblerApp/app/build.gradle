// Automatically increment version code by 1 each build
def Properties versionProps = new Properties()
def versionPropsFile = file('version.properties')
if (versionPropsFile.exists())
    versionProps.load(new FileInputStream(versionPropsFile))
def code = (versionProps['VERSION_CODE'] ?: "0").toInteger() + 1
versionProps['VERSION_CODE'] = code.toString()
versionProps.store(versionPropsFile.newWriter(), null)

// Gradle experimental format
apply plugin: 'com.android.model.application'

model {
    android {
        // Use latest and greatest version
        compileSdkVersion 24
        buildToolsVersion "24.0.1"

        defaultConfig {
            applicationId "org.tribler.android"
            // Android Beam supports large payload transfers
            // over Bluetooth since api level 16
            minSdkVersion.apiLevel 16
            targetSdkVersion.apiLevel 24
            // Auto increment, see above
            versionCode code
            // Used for python files versioning
            versionName "build " + code
            // Backwards compatibility for vector graphics
            vectorDrawables.useSupportLibrary = true
            // Java 8 features
            jackOptions {
                enabled = true
            }
            // Gradle experimental create build
            buildConfigFields {
                create() {
                    type "int"
                    name "VALUE"
                    value "1"
                }
            }
        }
        // NDK config mirrors python-for-android toolchain config
        ndk {
            abiFilters.add("armeabi-v7a")
            moduleName = "main"
            toolchain = "gcc"
            toolchainVersion = "4.9"
            platformVersion = 16
            stl = "gnustl_shared"
            renderscriptNdkMode = false
            CFlags.add("-I" + file("src/main/jni/include/python2.7"))
            ldFlags.add("-L" + file("src/main/jni/lib"))
            ldLibs.addAll(["log", "python2.7"])
        }
        // Configures source set directory.
        sources {
            main {
                // All native code, but start.c and pyjniusjni.c,
                // is already compiled in jniLibs, exclude src
                jni {
                    source {
                        exclude "**/winsock_pointers.c"
                        exclude "**/iocpsupport.c"
                        exclude "**/portmap.c"
                    }
                }
                // References prebuilt libraries defined below
                jniLibs {
                    dependencies {
                        library "gnustl_shared"
                        library "crypto"
                        library "ssl"
                        library "boost_system"
                        library "boost_date_time"
                        library "boost_python"
                        library "ffi"
                        library "sodium"
                        library "torrent_rasterbar"
                        library "leveldb"
                        library "sqlite3"
                    }
                }
            }
        }
    }
    repositories {
        // Prebuilt with python-for-android toolchain
        libs(PrebuiltLibraries) {
            boost_date_time {
                binaries.withType(SharedLibraryBinary) {
                    sharedLibraryFile = file("src/main/jniLibs/armeabi-v7a/libboost_date_time.so")
                }
            }
            boost_python {
                binaries.withType(SharedLibraryBinary) {
                    sharedLibraryFile = file("src/main/jniLibs/armeabi-v7a/libboost_python.so")
                }
            }
            boost_system {
                binaries.withType(SharedLibraryBinary) {
                    sharedLibraryFile = file("src/main/jniLibs/armeabi-v7a/libboost_system.so")
                }
            }
            crypto {
                binaries.withType(SharedLibraryBinary) {
                    sharedLibraryFile = file("src/main/jniLibs/armeabi-v7a/libcrypto1.0.2h.so")
                }
            }
            ffi {
                binaries.withType(SharedLibraryBinary) {
                    sharedLibraryFile = file("src/main/jniLibs/armeabi-v7a/libffi.so")
                }
            }
            gnustl_shared {
                binaries.withType(SharedLibraryBinary) {
                    sharedLibraryFile = file("src/main/jniLibs/armeabi-v7a/libgnustl_shared.so")
                }
            }
            leveldb {
                binaries.withType(SharedLibraryBinary) {
                    sharedLibraryFile = file("src/main/jniLibs/armeabi-v7a/libleveldb.so")
                }
            }
            sodium {
                binaries.withType(SharedLibraryBinary) {
                    sharedLibraryFile = file("src/main/jniLibs/armeabi-v7a/libsodium.so")
                }
            }
            sqlite3 {
                binaries.withType(SharedLibraryBinary) {
                    sharedLibraryFile = file("src/main/jniLibs/armeabi-v7a/libsqlite3.so")
                }
            }
            ssl {
                binaries.withType(SharedLibraryBinary) {
                    // Android does not have regular versioning of .so files, but does have an
                    // old version of openssl build in which prevents the loading libssl.so
                    // included with the app, therefore a custom name with version was introduced
                    sharedLibraryFile = file("src/main/jniLibs/armeabi-v7a/libssl1.0.2h.so")
                }
            }
            torrent_rasterbar {
                binaries.withType(SharedLibraryBinary) {
                    // When the python bindings shared library and the native one have the same name
                    // it causes a cyclic dependency reference on runtime, therefore the native
                    // .so file was renamed to include _rasterbar
                    sharedLibraryFile = file("src/main/jniLibs/armeabi-v7a/libtorrent_rasterbar.so")
                }
            }
        }
    }
}
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    testCompile 'com.squareup.okhttp3:mockwebserver:3.4.1'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    compile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2' //DEBUG
    //compile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    //compile 'com.facebook.stetho:stetho:1.3.1' //DEBUG
    //compile 'com.facebook.stetho:stetho-okhttp3:1.3.1' //DEBUG
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.8'
    compile 'com.cantrowitz:rxbroadcast:1.0.0'
    compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'
    compile 'com.jakewharton.rxbinding:rxbinding-support-v4:0.4.0'
    compile 'com.jakewharton.rxbinding:rxbinding-appcompat-v7:0.4.0'
    compile 'com.jakewharton.rxbinding:rxbinding-design:0.4.0'
    compile 'com.jakewharton.rxbinding:rxbinding-recyclerview-v7:0.4.0'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.android.support:support-v4:24.1.1'
    compile 'com.android.support:appcompat-v7:24.1.1'
    compile 'com.android.support:design:24.1.1'
    compile 'com.android.support:recyclerview-v7:24.1.1'
    compile 'xyz.danoz:recyclerviewfastscroller:0.1.3'
    compile 'com.jakewharton:butterknife:8.1.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.1.0'
}
